cmake_minimum_required (VERSION 2.8.10)
PROJECT (HDF4_MFHDF_FORTRAN C CXX Fortran)

INCLUDE_DIRECTORIES (
    ${CMAKE_Fortran_MODULE_DIRECTORY}
    ${HDF4_HDF_BINARY_DIR}
    ${HDF4_MFHDF_FORTRAN_BINARY_DIR}
    ${HDF4_HDFSOURCE_DIR}
    ${HDF4_MFHDFSOURCE_DIR}
    ${HDF4_HDF_TESTSOURCE_DIR}
)

ADD_DEFINITIONS (-DHDF)
   
IF (APPLE)
  STRING (TOLOWER APPLE mf_f90_sys)
ELSE (APPLE)
  IF (${CMAKE_SYSTEM_NAME} MATCHES "SunOS")
    STRING (TOLOWER Sun mf_f90_sys)
  ELSE (${CMAKE_SYSTEM_NAME} MATCHES "SunOS")  
    STRING (TOLOWER ${CMAKE_SYSTEM_NAME} mf_f90_sys)
  ENDIF (${CMAKE_SYSTEM_NAME} MATCHES "SunOS")
ENDIF (APPLE)

IF (HDF4_ENABLE_NETCDF)
  ADD_CUSTOM_COMMAND (
      OUTPUT ${HDF4_BINARY_DIR}/ftest.f
      COMMAND ${CMAKE_COMMAND} -E copy ${HDF4_MFHDF_FORTRAN_SOURCE_DIR}/config/ftest-${mf_f90_sys}.f ${HDF4_BINARY_DIR}/ftest.f
      COMMENT " Copying ftest Name ftest-${mf_f90_sys}.f to ftest.f"
  )

  ADD_CUSTOM_COMMAND (
      OUTPUT ${HDF4_BINARY_DIR}/jackets.c
      COMMAND ${CMAKE_COMMAND} -E copy ${HDF4_MFHDF_FORTRAN_SOURCE_DIR}/config/jackets-${mf_f90_sys}.c ${HDF4_BINARY_DIR}/jackets.c
      COMMENT " Copying jackets Name jackets-${mf_f90_sys}.c to jackets.c"
  )
  ADD_CUSTOM_COMMAND (
      OUTPUT ${HDF4_BINARY_DIR}/netcdf.h
      COMMAND ${CMAKE_COMMAND} -E copy ${HDF4_MFHDF_LIBSRC_SOURCE_DIR}/config/netcdf-${mf_f90_sys}.h ${HDF4_BINARY_DIR}/netcdf.h
      COMMENT " Copying netcdf Name netcdf-${mf_f90_sys}.h to netcdf.h"
  )
ELSE (HDF4_ENABLE_NETCDF)
  ADD_CUSTOM_COMMAND (
      OUTPUT ${HDF4_BINARY_DIR}/hdf4_netcdf.h
      COMMAND ${CMAKE_COMMAND} -E copy ${HDF4_MFHDF_LIBSRC_SOURCE_DIR}/config/netcdf-${mf_f90_sys}.h ${HDF4_BINARY_DIR}/hdf4_netcdf.h
      COMMENT " Copying netcdf Name netcdf-${mf_f90_sys}.h to hdf4_netcdf.h"
  )
ENDIF (HDF4_ENABLE_NETCDF)

ADD_CUSTOM_COMMAND (
   OUTPUT ${HDF4_BINARY_DIR}/netcdf.inc
   COMMAND ${CMAKE_COMMAND} -E copy ${HDF4_MFHDF_FORTRAN_SOURCE_DIR}/config/netcdf-${mf_f90_sys}.inc ${HDF4_BINARY_DIR}/netcdf.inc
   COMMENT " Copying netcdf Name netcdf-${mf_f90_sys}.inc to netcdf.inc")

IF (HDF4_ENABLE_NETCDF)
  SET (HDF4_MFHDF_LIBSRC_CSTUB_FSRCS ${HDF4_BINARY_DIR}/jackets.c ${HDF4_MFHDF_FORTRAN_SOURCE_DIR}/mfsdf.c)
ELSE (HDF4_ENABLE_NETCDF)
  SET (HDF4_MFHDF_LIBSRC_CSTUB_FSRCS ${HDF4_MFHDF_FORTRAN_SOURCE_DIR}/mfsdf.c)
ENDIF (HDF4_ENABLE_NETCDF)

SET (HDF4_MFHDF_LIBSRC_CSTUB_FHDRS 
    ${HDF4_MFHDFSOURCE_DIR}/hdf2netcdf.h 
    ${HDF4_MFHDFSOURCE_DIR}/local_nc.h 
    ${HDF4_MFHDFSOURCE_DIR}/mfhdf.h 
    ${HDF4_MFHDFSOURCE_DIR}/mfhdfi.h
)
IF (HDF4_ENABLE_NETCDF)
#  SET (HDF4_MFHDF_LIBSRC_CSTUB_FSRCS ${HDF4_MFHDF_LIBSRC_CSTUB_FSRCS} ${HDF4_BINARY_DIR}/jackets.c)
  SET (HDF4_MFHDF_LIBSRC_CSTUB_FHDRS ${HDF4_MFHDF_LIBSRC_CSTUB_FHDRS} ${HDF4_BINARY_DIR}/netcdf.h)
ELSE (HDF4_ENABLE_NETCDF)
  SET (HDF4_MFHDF_LIBSRC_CSTUB_FHDRS ${HDF4_MFHDF_LIBSRC_CSTUB_FHDRS} ${HDF4_BINARY_DIR}/hdf4_netcdf.h)
ENDIF (HDF4_ENABLE_NETCDF)

SET (FORTRAN_SRC_DIR ${CMAKE_CURRENT_SOURCE_DIR})

#-----------------------------------------------------------------------------
# Add Main fortran library
#-----------------------------------------------------------------------------
ADD_LIBRARY (${HDF4_MF_FCSTUB_LIB_TARGET} ${LIB_TYPE} ${HDF4_MFHDF_LIBSRC_CSTUB_FSRCS} ${HDF4_MFHDF_LIBSRC_CSTUB_FHDRS} ${HDF4_HDF_SRC_CHDRS} ${HDF4_HDFSOURCE_DIR}/hproto_fortran.h)
SET_TARGET_PROPERTIES (${HDF4_MF_FCSTUB_LIB_TARGET} PROPERTIES LINKER_LANGUAGE C)
IF (WIN32 AND NOT CYGWIN)
  ADD_DEFINITIONS (-DDOS_FS)
ENDIF (WIN32 AND NOT CYGWIN)
IF (HDF4_BUILD_XDR_LIB)
  IF (WIN32 AND NOT CYGWIN)
    ADD_DEFINITIONS (-DNO_SYS_XDR_INC)
  ENDIF (WIN32 AND NOT CYGWIN)
  INCLUDE_DIRECTORIES (${HDF4_MFHDF_XDR_DIR})
  TARGET_LINK_LIBRARIES (${HDF4_MF_FCSTUB_LIB_TARGET} ${HDF4_MF_XDR_LIB_TARGET})
ENDIF (HDF4_BUILD_XDR_LIB)
TARGET_C_PROPERTIES (${HDF4_MF_FCSTUB_LIB_TARGET} " " " ")
TARGET_LINK_LIBRARIES (${HDF4_MF_FCSTUB_LIB_TARGET} ${HDF4_MF_LIB_TARGET} ${HDF4_SRC_LIB_TARGET})
SET_GLOBAL_VARIABLE (HDF4_LIBRARIES_TO_EXPORT "${HDF4_LIBRARIES_TO_EXPORT};${HDF4_MF_FCSTUB_LIB_TARGET}")
H4_SET_LIB_OPTIONS (${HDF4_MF_FCSTUB_LIB_TARGET} ${HDF4_MF_FCSTUB_LIB_NAME} ${LIB_TYPE})

SET (HDF4_MF_FORTRAN_SRCS 
    ${HDF4_MFHDF_FORTRAN_DIR}/mfsdff.f
)
#-----------------------------------------------------------------------------
ADD_LIBRARY (${HDF4_MF_FORTRAN_LIB_TARGET} ${LIB_TYPE} ${HDF4_MF_FORTRAN_SRCS})
SET (SHARED_LINK_FLAGS " ")
IF (WIN32)
  IF (${LIB_TYPE} MATCHES "SHARED")
    IF (MSVC)
      SET (SHARED_LINK_FLAGS "/DLL /DEF:${HDF4_MFHDF_FORTRAN_SOURCE_DIR}/mfhdf_fortrandll.def")
    ENDIF (MSVC)
  ENDIF (${LIB_TYPE} MATCHES "SHARED")
ENDIF (WIN32)
TARGET_FORTRAN_PROPERTIES (${HDF4_MF_FORTRAN_LIB_TARGET} " " ${SHARED_LINK_FLAGS})
SET_TARGET_PROPERTIES (${HDF4_MF_FORTRAN_LIB_TARGET} PROPERTIES LINKER_LANGUAGE Fortran)
TARGET_LINK_LIBRARIES (${HDF4_MF_FORTRAN_LIB_TARGET} ${HDF4_MF_FCSTUB_LIB_TARGET} ${LINK_LIBS})
SET_GLOBAL_VARIABLE (HDF4_LIBRARIES_TO_EXPORT "${HDF4_LIBRARIES_TO_EXPORT};${HDF4_MF_FORTRAN_LIB_TARGET}")
H4_SET_LIB_OPTIONS (${HDF4_MF_FORTRAN_LIB_TARGET} ${HDF4_MF_FORTRAN_LIB_NAME} ${LIB_TYPE})

IF (BUILD_TESTING)
  INCLUDE (CMakeTests.cmake)
ENDIF (BUILD_TESTING)

#-----------------------------------------------------------------------------
# Add library to CMake Install : Installs lib and cmake config info
#-----------------------------------------------------------------------------

INSTALL_TARGET_PDB (${HDF4_MF_FCSTUB_LIB_TARGET} ${HDF4_INSTALL_LIB_DIR} fortlibraries)
INSTALL_TARGET_PDB (${HDF4_MF_FORTRAN_LIB_TARGET} ${HDF4_INSTALL_LIB_DIR} fortlibraries)
  
INSTALL (
    TARGETS 
        ${HDF4_MF_FCSTUB_LIB_TARGET}
        ${HDF4_MF_FORTRAN_LIB_TARGET}
    EXPORT 
        ${HDF4_EXPORTED_TARGETS}
    LIBRARY DESTINATION ${HDF4_INSTALL_LIB_DIR} COMPONENT fortlibraries 
    ARCHIVE DESTINATION ${HDF4_INSTALL_LIB_DIR} COMPONENT fortlibraries
    RUNTIME DESTINATION ${HDF4_INSTALL_BIN_DIR} COMPONENT fortlibraries
)
